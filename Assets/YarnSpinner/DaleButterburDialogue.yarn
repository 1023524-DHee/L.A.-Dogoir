title: DaleButterburDialogue
tags:NPC, Detective
position: -522,-132
---
<<if $DaleIsInitialGreeting == true>>

Dale: I can’t believe this is happening in my bar. Whatever you need, Detective, you just let me know.
Detective: I’d like to ask you some questions, actually.
Dale: Of course.
<<set $DaleIsInitialGreeting to false>>

<<elseif $DaleIsInitialGreeting == false>>

<<set $DaleRandomGreeting to random_range(0,3)>>
<<if $DaleRandomGreeting == 0 >>
<<Animate "Wave">>
Dale: How can I help, Detective?
<<elseif $DaleRandomGreeting == 1>>
Dale: Any leads, Detective?
<<elseif $DaleRandomGreeting == 2>>
Dale: What do you need?
<<elseif $DaleRandomGreeting == 3>>
Dale: How’s the investigation going?
<<endif>>

<<endif>>

<<jump Questions>>

===
title: Questions
position: -307,-131
---
-> Detective: Who are you?
    <<jump WhoAreYou>>

-> Detective: Gunshot?
    <<jump Gunshot>>

-> Detective: Jack Harlow?
    <<jump DoYouKnowJack>>

-> Detective: Enemies?
    <<jump WhoWantsJackDead>>

<<if $Peggy == true>> 
-> Detective:  Peggy?
    <<jump Peggy>>
<<endif>>

<<if $Loretta == true>> 
-> Detective:  Loretta?
    <<jump Loretta>>
<<endif>>

<<if $Johhny == true>> 
-> Detective:  Johnny?
    <<jump Johhny>>
<<endif>>

<<if $DetectiveTriedToOpenDoor == true>>
-> Detective: The door?
<<jump Storeroom>>
<<endif>>

<<if $DetectiveHasFoundShotgun == true>>
-> Detective: About that there shotgun
<<jump Shotgun>>
<<endif>>

<<if $DetectiveHasFoundDrugs == true>>
-> Detective: We found the drugs Dale
<<jump Drugs>>
<<endif>>

<<if $DetectiveHasFoundFlask == true>>
<<if $JohnnyHasSpokenAboutFlask == true>>
-> Detective: Peggy visits often?
<<jump PeggyVisitsOften>>
<<endif>>
<<endif>>

-> Detective: Goodbye
<<jump Goodbye>>

===

title: WhoAreYou
position: 204,-140
---
Detective: Who are you, exactly?
Dale: Dale Butterbur, I own this bar. It was used by bootleggers during the prohibition, would you believe, but the old owner went to the big house for brewing his own hooch, and viola, it was mine for the buying, right as the prohibition ended.

<<jump Questions>>
===

title: Gunshot
position: -77,-430
---
Detective: Tell me exactly where you were when the gunshot occurred?
Dale: The lights had just gone out, so I was in the storeroom turning the breaker back on. Faulty wiring, it trips every now and then.

<<jump Lights>>
===
title: Lights
position: 237,-418
---
Detective: When you turned the lights back on, did you see anyone around the body, anyone acting suspiciously?
Dale: Now that you mention it, Loretta was the closest to Harlow when I came back out. That was odd as she hadn’t been in the room when I left.

<<set $Dale to true>>
<<set $DaleKnowsAboutLoretta to true>>
<<jump Questions>>
===

title: DoYouKnowJack
position: 191,-236
---
Detective: How well do you know Jack Harlow?
Dale: Not particularly well. He was a semi-regular here, I guess you could say. But to be honest with you detective, he was a real drip. A real greaseball, from what I could tell. Never liked to serve him, but a few lincolns is a few lincolns, no matter the customer.

<<set $Dale to true>>
<<jump Questions>>
===
title: WhoWantsJackDead
position: 216,-49
---
Detective: Do you know of anyone here who would want Jack Harlow dead?
Dale: Well, he didn’t seem like a stand-up gent in general. But I just can’t imagine someone would want to kill him, even so.

<<set $Dale to true>>
<<jump Questions>>
===

title: Peggy
position: 209,53
---
Detective: How well do you know Peggy Harlow?
Dale: I hardly know her at all. She came in sometimes to pick up her drunk excuse for a husband. I felt sorry for her, honestly.

<<jump Questions>>
===

title: Loretta
position: 171,192
---
Detective: What do you know about Loretta Dubois?
Dale: She performs here once a week - she’s a great singer. Obviously she’s a bit of a dish too, which brings in the customers. I think Harlow got it for her bad. Never missed a performance. He was more likely to pay her just for existing than me was to pay me for booze.

<<jump Questions>>
===

title: Johhny
position: 181,302
---
Detective: What’s the deal with Johnny Malone?
Dale: I know he looks rough, but he’s a swell guy. He’s got a little share in the bar with me, so we’re sort of like business partners. He’s my security on busier nights. He’s got such a sour puss people tend to behave when he’s around. Until now, I guess…

===
title: Storeroom
position: 141,457
---
Detective: Can you let me into the storeroom? I’d like to look around.
Dale: I’d love to help, Detective, but I’m a little busy right now. I’ll open it up when I’ve cleaned up the mess here.

<<jump Questions>>
===
title: Shotgun
position: 141,457
---
Detective: You gotta license for that buttercup?
Dale: Yeah 
Detective: Do you have any other weapons I should know about 
Dale: oh yeah, i gave one to ms.dubois to look after herself, lotsa unsavourvy characters hanging about.
<<set $DaleHasSpokenAboutShotgun to true>>

<<jump Questions>>
===
title: Drugs
position: 141,457
---
Detective: You realise that’s illegal right fella?
Dale: we’ll ive got some friends in the force that’ll vouch for me
<<set $DaleHasSpokenAboutDrugs to true>>

<<jump Questions>>
===
title: PeggyVisitsOften
position: 141,457
---
Detective: You enjoy Peggy coming around to pick up Jack?
Dale: Hey I dont mind seeing that piece of ass swinging around
<<set $DaleHasSpokenAboutPeggy to true>>

<<jump Questions>>
===

title: Goodbye
position: -211,486
---

<<set $DaleRandomGoodbye to random_range(0,2)>>
<<if $DaleRandomGoodbye == 0>>
<<Animate Dale "GoodbyeWave">>
Detective: Thanks for your help
Dale: Anytime!
<<elseif $DaleRandomGoodbye == 1>>
<<Animate Dale "GoodbyeWave">>
Detective: I appreciate your help.
Dale: No problems at all, Detective.
<<elseif $DaleRandomGoodbye == 2>>
<<Animate Dale "GoodbyeWave">>
Detective: That’s all for now.
Dale: Bye.
<<endif>>

<<stop>>
===