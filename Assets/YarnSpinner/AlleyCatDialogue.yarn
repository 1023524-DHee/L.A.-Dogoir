title: AlleyCatDialogue
tags:NPC, Detective, Dog
position: -338,-113
---
<<if $OMalleyIsInitialGreeting == false>>

OMalley: I don’t do the whole dog-cat thing so don’t even try, pal. I could eat a mutt like you for breakfast.
Archie: I’m just here to ask you some questions, sir, about the gunshot that sounded earlier.
OMalley: Oh. I don’t know much about that.
<<set $OMalleyIsInitialGreeting to true>>

<<elseif $OMalleyIsInitialGreeting == true>>

<<set $OMalleyRandomGreeting to random_range(0,2)>>
<<if $OMalleyRandomGreeting == 0 >>
<<Animate "Wave">>
OMalley: What do you want now?
<<elseif $OMalleyRandomGreeting == 1>>
OMalley: You don’t quit, do you?
<<elseif $OMalleyRandomGreeting == 2>>
OMalley: Can’t you see I’m busy?
<<endif>>

<<endif>>

<<jump OMalleyQuestions>>
===
title: OMalleyQuestions
position: 69,-84
---
-> Detective: Who are you?
<<jump OMalleyWhoAreYou>>


<<if $OMalleyAskedForName == true>> 
-> Detective: Gunshot?
<<jump OMalleyGunshot>>
<<endif>>

<<if $OMalleyAskedAboutThrow == true>> 
-> Detective: What did they throw at you??
<<jump OMalleyWhatDidTheyThrow>>
<<endif>>

<<if $OMalleyAskedGetIt == true>> 
-> Detective: Can you get it?
<<jump OMalleyCanYouGetIt>>
<<endif>>



-> Detective: Goodbye
<<jump OMalleyGoodbye>>

===

title: OMalleyWhoAreYou
position: 283,-340
---
Archie: You got a name?
OMalley: Yeah, O’Malley.

<<set $OMalleyAskedForName = true>>

<<jump OMalleyQuestions>>
===

title: OMalleyGunshot
position: -111,-477
---

Archie: Were you out here when the gunshot rang out, Mr. O’Malley?
OMalley: Well they don’t let me inside, do they? Only you dogs get that pleasure. Yeah, I was here. I was trying to dig through these bins, find a little dinner. Next thing you know, ka-boom! Scared the hairballs out of me… finally got back to the task at hand and some damn human comes out here and throws something at me. What a night, huh!
Archie: Some human?
OMalley: I don’t know, I was a bit dazed on account of their aim. Only saw the back of them as they went back inside. But I smelt them - they smelt like a garden. Hard to miss a smell like that out here.

<<set $OMalleyAskedAboutThrow = true>>
<<jump OMalleyQuestions>>

===

title: OMalleyWhatDidTheyThrow
position: -111,-477
---

Archie: What did they throw at you?
OMalley: A little tube or something, I don’t know. It’s in this here bin somewhere.
<<set $OMalleyAskedGetIt = true>>
<<jump OMalleyQuestions>>
===

title: OMalleyCanYouGetIt
position: -111,-477
---

Archie: Can you get it for me?
OMalley: Now why would I do extra work for something that got nothing to do with me?
Archie: I’ll get it myself then.
OMalley: I don’t think so. This is my turf, pal. I’ve answered your dumb questions real nice, so how about you leave me alone? I still got food to find.

<<set $OMalleyWillNotGetSyringe to true>>
//player needs to lead cat away with rat

<<jump OMalleyQuestions>>
===

title: OMalleyGoodbye
position: -191,263
---

<<set $OMalleyRandomGoodbye to random_range(0,2)>>
<<if $OMalleyRandomGoodbye == 0 >>
<<Animate Dale "GoodbyeWave">>
Archie: Thanks.
OMalley: Whatever.

<<elseif $OMalleyRandomGoodbye == 1>>
<<Animate Dale "GoodbyeWave">>
Archie: That’s all for now.
OMalley: Now and forever, I hope.

<<elseif $OMalleyRandomGoodbye == 2>>
<<Animate Dale "GoodbyeWave">>
Archie: I appreciate it.
OMalley: Well I don’t.

<<endif>>

<<stop>>
===