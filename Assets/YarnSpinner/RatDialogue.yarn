title: RatDialogue
tags:NPC, Detective, Dog
position: -338,-113
---
<<if $RatIsInitialGreeting == false>>

Rat: What the hell d’you want?
Archie: Pleased to meet you, too. I’d like to ask you some questions about the murder that’s just taken place.
Rat: Hmmph.

<<set $RatIsInitialGreeting to true>>

<<elseif $RatIsInitialGreeting == true>>

<<set $RatRandomGreeting to random_range(0,2)>>
<<if $RatRandomGreeting == 0 >>
<<Animate "Wave">>
Rat: What now?
<<elseif $RatRandomGreeting == 1>>
Rat: What?
<<elseif $RatRandomGreeting == 2>>
Rat: You just don’t get the message, do you?
<<endif>>

<<endif>>

<<jump RatQuestions>>
===
title: RatQuestions
position: 69,-84
---
-> Detective: Who are you?
<<jump RatWhoAreYou>>

-> Detective: Gunshot?
<<jump RatGunshot>>

-> Detective: Did you see anything?
<<jump RatDidYouSeeAnything>>


<<if $ArchieFoundTheCheese == true>> 
-> Detective:  I've got some cheese for you
<<jump RatFoundTheCheese>>
<<endif>>

<<if $ArchieGaveCheese == true>> 
<<if $OMalleyWillNotGetSyringe == true>> 
-> Detective:  I saw some cheese outside...
<<jump RatSawCheeseOutside>>
<<endif>>
<<endif>>

<<if $RattusMovedOutside == true>> 
-> Detective:  What are you doing out here?
<<jump RatMovedToBin>>
<<endif>>

-> Detective: Goodbye
<<jump RatGoodbye>>

===

title: RatWhoAreYou
position: 283,-340
---
Archie: Who are you, exactly?
Rat: I’m Rattus and Rattus is me.

<<jump RatQuestions>>
===

title: RatGunshot
position: -111,-477
---

Archie: Where were you when the shot rang out, Rattus?
Rat: Rattus was right ‘ere.

<<jump RatQuestions>>

===

title: RatDidYouSeeAnything
position: -111,-477
---

Archie: If you were here, you must have seen something?
Rat: You sure ask a lotta questions. Maybe Rattus saw somethin’, maybe he didn’t. What’s in it for him if he tells you what he saw? Tell ya what, I’m awfully hungry. Find Rattus some food and then maybe he’ll talk.
Archie: Some food?
Rat: Yeah, yeah… some cheese would be nice. Run along now, little pet.

<<jump RatQuestions>>
===

title: RatFoundTheCheese
position: -111,-477
---

Archie: I got you some cheese like you asked.
Rat: Give it ‘ere!
Archie: Not so fast. Tell me what you know, and then I’ll give you the cheese.
Rat: Hmph. Fine. Rattus saw two of them humans, saying somethin’ about a safe.
Archie: A safe? What exactly did they say?
Rat: They was talkin’ about the code on account of one of them forgettin’. 
Archie: And? Did they say what the code was?
Rat: Yeah, it was BLPO.
Archie: Thanks for your help, Rattus, here’s the cheese.
Rat: ‘Bout time. Give it ‘ere!

<<set $ArchieGaveCheese to true>>
<<jump RatQuestions>>
===
title:  RatSawCheeseOutside
position: -111,-477
---
Archie: Hey, I saw some more cheese outside…
Rattus: Really? 
Archie: Yeah, a whole wheel. You should have a look.
Rattus: Outta the way!

//make rattus move to outside
<<set $RattusMovedOutside to true>>

<<jump RatQuestions>>
===
title:  RatMovedToBin
position: -111,-477
---
Archie: Uh…
Rattus: Surprised to see me, aren’t ya, pup? Betcha thought your little lie about the cheese would lead ol’ Rattus to his death, didn’t ya? There ain’t no cat Rattus can’t lick, no sirree! I should thank ya, really, first a bit of cheese and now a good fight! Rattus hasn’t felt so alive in weeks!
Archie: Uh…glad to help. Is the cat ok?
Rattus: Rattus sent that ol’ puss howlin’ all the way home! Hehehe.

<<wait 2>>

Archie: Did the cat leave anything behind?
Rattus: Left this ‘ere stick thing.
Archie: May I have it?
Rattus: Well Rattus sure don’t want it.

<<set $DetectiveHasFoundSyringe to true>>
<<jump RatQuestions>>
===

title: RatGoodbye
position: -191,263
---

<<set $RatRandomGoodbye to random_range(0,2)>>
<<if $RatRandomGoodbye == 0 >>
<<Animate Dale "GoodbyeWave">>
Archie: Thanks.
Rat: Whatever.

<<elseif $RatRandomGoodbye == 1>>
<<Animate Dale "GoodbyeWave">>
Archie: That’s all for now.
Rat: Only for now? Drat.

<<elseif $RatRandomGoodbye == 2>>
<<Animate Dale "GoodbyeWave">>
Archie: I appreciate it.
Rat: Well I don’t.

<<endif>>

<<stop>>
===